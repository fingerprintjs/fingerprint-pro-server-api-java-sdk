apply plugin: 'org.openapi.generator'
apply plugin: 'com.diffplug.spotless'

group = 'com.fingerprint'
version = '5.1.1'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.+'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.3.0'
        classpath "org.openapitools:openapi-generator-gradle-plugin:6.6.0"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    publications {
        maven(MavenPublication) {
           artifactId = 'fingerprint-pro-server-api-sdk'
           artifact jar
        }
        localPublication(MavenPublication) {
            artifact jar
            groupId 'com.fingerprint'
            artifactId 'fingerprint-pro-server-api-sdk'
            version '5.1.1'
        }
    }
}

tasks.register('execute', JavaExec) {
   main = System.getProperty('mainClass')
   classpath = sourceSets.main.runtimeClasspath
}

dependencies {
    implementation libs.swagger.annotations
    implementation libs.jsr305
    api libs.jersey.client
    api libs.jersey.hk2
    api libs.jersey.media.json
    api libs.jersey.media.multipart
    api libs.jersey.apache.connector
    api libs.jackson.core
    api libs.jackson.annotations
    api libs.jackson.databind
    api libs.jackson.databind.nullable
    api libs.jackson.jsr310
    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter.engine
    testImplementation libs.mockito
}

test {
    useJUnitPlatform()
}

javadoc {
    options.tags = [ "http.response.details:a:Http Response Details" ]
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/examples/java'
        }
    }
}

// Use spotless plugin to automatically format code, remove unused import, etc
// To apply changes directly to the file, run `gradlew spotlessApply`
// Ref: https://github.com/diffplug/spotless/tree/main/plugin-gradle
spotless {
    // comment out below to run spotless as part of the `check` task
    enforceCheck false

    format 'misc', {
        // define the files (e.g. '*.gradle', '*.md') to apply `misc` to
        target '.gitignore'
        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces() // Takes an integer argument if you don't like 4
        endWithNewline()
    }
    java {
        // don't need to set target, it is inferred from java
        // apply a specific flavor of google-java-format
        googleJavaFormat('1.8').aosp().reflowLongStrings()
        removeUnusedImports()
        importOrder()
    }
}

jar {
    exclude 'com/fingerprint/example/**'
    duplicatesStrategy 'exclude'
}

openApiGenerate {
    generatorName.set("java")
    inputSpec.set("$rootDir/res/fingerprint-server-api.yaml")
    outputDir.set("$rootDir")
    groupId.set("com.fingerprint")
    id.set("fingerprint-pro-server-api-sdk")
    version.set("5.1.1")
    apiPackage.set("com.fingerprint.api")
    modelPackage.set("com.fingerprint.model")
    invokerPackage.set("com.fingerprint.sdk")
    library.set("jersey2")

    gitHost.set("github.com")
    gitRepoId.set("fingerprint-pro-server-api-java-sdk")
    gitUserId.set("fingerprintjs")
    configOptions.put("hideGenerationTimestamp", "true")

    ignoreFileOverride.set("build.gradle")
}

tasks.register('runFunctionalTests', JavaExec) {
    group = "execute"
    description = "Run the Functional Tests"
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.fingerprint.example.FunctionalTests'
}

tasks.register('removeWrongDocumentationLinks') {
    doLast {
        fileTree("./docs").visit { FileVisitDetails details ->
            if (details.file.isFile()) {
                def fileContents = details.file.text
                fileContents = fileContents.replace("[**OffsetDateTime**](OffsetDateTime.md)", "**OffsetDateTime**")
                fileContents = fileContents.replace("[**URI**](URI.md)", "**URI**")
                details.file.text = fileContents
            }
        }
    }
}
